plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group = 'one.microproject.iamservice'
archivesBaseName = "iam-model"
version = '2.4.2-RELEASE'

sourceCompatibility = '11'

repositories {
    mavenCentral()
}

dependencies {
    implementation('com.fasterxml.jackson.core:jackson-annotations:2.11.3')
    implementation('com.fasterxml.jackson.core:jackson-core:2.11.3')
    implementation('com.fasterxml.jackson.core:jackson-databind:2.11.3')

    testImplementation('org.junit.jupiter:junit-jupiter:5.7.0')
    testImplementation('org.junit.jupiter:junit-jupiter-api:5.7.0')
    testImplementation('org.junit.jupiter:junit-jupiter-engine:5.7.0')
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

ext.isSnapshot = version.endsWith("-SNAPSHOT")
if (!project.hasProperty('ossrhUsername') || !project.hasProperty('ossrhPassword')) {
    ext.ossrhUsername = "none";
    ext.ossrhPassword = "****";
    ext.isPublishVersion = false;
} else {
    ext.isPublishVersion = true;
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

tasks.withType(Sign) {
    onlyIf { isPublishVersion }
}

publishing {
    publications {
        mavenJava(MavenPublication) {

            groupId = 'one.microproject.iamservice'
            artifactId = 'iam-model'
            //version = ...

            from components.java
            artifact(sourceJar) {
                classifier = 'sources'
            }
            artifact(javadocJar) {
                classifier = 'javadoc'
            }

            pom {
                name = 'IAM-service common model'
                description = 'Common data model, DTOs and Utils for IAM-service.'
                url = 'https://github.com/jveverka/iam-service'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/jveverka/iam-service/blob/2.x.x/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'jveverka'
                        name = 'Juraj Veverka'
                        email = 'gergej123@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:jveverka/iam-service.git'
                    developerConnection = 'scm:git:git@github.com:jveverka/iam-service.git'
                    url = 'https://github.com/jveverka/iam-service'
                }
            }
        }
    }
    repositories {
        maven {
            if (isSnapshot) {
                url "https://oss.sonatype.org/content/repositories/snapshots"
            } else {
                url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            }
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}

signing {
    sign configurations.archives
    sign publishing.publications.mavenJava
}

model {
    tasks.generatePomFileForMavenJavaPublication {
        destination = file("$buildDir/generated-pom.xml")
    }
    tasks.publishMavenJavaPublicationToMavenLocal {
        dependsOn project.tasks.signArchives
    }
    tasks.publishMavenJavaPublicationToMavenRepository {
        dependsOn project.tasks.signArchives
    }
}
